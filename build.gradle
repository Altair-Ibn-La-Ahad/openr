plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'io.freefair.lombok' version '5.0.1'
    id 'io.wusa.semver-git-plugin' version '2.0.2'
}

javafx {
    version = '11'
    modules = ['javafx.controls', 'javafx.fxml']
}

wrapper {
    gradleVersion = '6.3'
    distributionType = Wrapper.DistributionType.ALL
}

sourceCompatibility = JavaVersion.VERSION_11

group 'pl.greywarden.openr'

semver {
    snapshotSuffix = "SNAPSHOT"
    dirtyMarker = "dirty"
    branches {
        branch {
            regex = ".+"
            incrementer = "MINOR_INCREMENTER"
            formatter = { "${semver.info.version.major}.${semver.info.version.minor}.${semver.info.version.patch}+build.${semver.info.count}" }
        }
    }
}
version = semver.info

mainClassName = 'pl.greywarden.openr.OpenR'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'

    implementation 'de.saxsys:mvvmfx:1.8.0'
    implementation 'de.saxsys:mvvmfx-spring-boot:1.8.0'

    implementation 'org.kordamp.ikonli:ikonli-javafx:11.3.4'
    implementation 'org.kordamp.ikonli:ikonli-material-pack:11.3.4'
    implementation 'org.kordamp.ikonli:ikonli-materialdesign-pack:11.3.4'

    implementation 'org.controlsfx:controlsfx:11.0.1'
}

processResources {
    doFirst {
        def resourcesDir = sourceSets.main.output.resourcesDir
        resourcesDir.mkdirs()
        new File(resourcesDir, "app-info.properties").text = "app-version=$version"
    }
}
